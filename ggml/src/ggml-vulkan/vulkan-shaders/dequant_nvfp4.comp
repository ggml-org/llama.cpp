#version 450

#include "dequant_head.glsl"

layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

layout (binding = 0) readonly buffer A {block_nvfp4 data_a[];};
layout (binding = 1) writeonly buffer D {D_TYPE data_b[];};

void main() {
    const uint i = gl_WorkGroupID.x * 4 + gl_LocalInvocationID.x / 64;

    init_iq_shmem(gl_WorkGroupSize);

    const uint tid = gl_LocalInvocationID.x % 64;
    const uint il  = tid/32;
    const uint ir  = tid%32;
    // 2 NVFP4 blocks per 32-element group, 32 threads handle 16 blocks
    const uint ib = 16*i + ir/2;
    if (ib >= p.nel / 16) {
        return;
    }

    const uint half_idx = ir % 2; // 0 = lo nibbles, 1 = hi nibbles
    const uint q_idx = 4*il;
    const uint b_idx = 16*ib + 8*half_idx + q_idx;

    const float d = float(data_a[ib].d);

    [[unroll]] for (uint l = 0; l < 4; ++l) {
        uint q = half_idx == 0 ? (data_a[ib].qs[q_idx + l] & 0xF) : (data_a[ib].qs[q_idx + l] >> 4);
        data_b[b_idx + l] = D_TYPE(d * float(kvalues_mxfp4[q]));
    }
}
