#version 450

#include "types.glsl"
#include "generic_binary_head.glsl"

layout(local_size_x = 512, local_size_y = 1, local_size_z = 1) in;

void main() {
    const uint idx = gl_GlobalInvocationID.x;
    if (idx >= p.ne) {
        return;
    }

    const uint offset = p.param3;
    const uint src1_i = idx - offset;
    const uint oz = src1_i / p.nb03;
    const uint remy = src1_i - oz * p.nb03;
    const uint oy = remy / p.nb02;
    const uint remx = remy - oy * p.nb02;
    const uint ox = remx / p.nb01;
    const uint ow = remx % p.nb01;

    uint i00, i01, i02, i03;
    get_indices(idx, i00, i01, i02, i03);

    if (ow < p.ne10 && ox < p.ne11 && oy < p.ne12 && oz < p.ne13) {
        data_d[get_doffset() + dst_idx(i00, i01, i02, i03)] = D_TYPE(FLOAT_TYPE(data_a[get_aoffset() + src0_idx(i00, i01, i02, i03)]) + FLOAT_TYPE(data_b[get_boffset() + ow + ox * p.ne10 + oy * p.ne10 * p.ne11 + oz * p.ne10 * p.ne11 * p.ne12]));
    } else {
        data_d[get_doffset() + dst_idx(i00, i01, i02, i03)] = D_TYPE(FLOAT_TYPE(data_a[get_aoffset() + src0_idx(i00, i01, i02, i03)]));
    }
}
